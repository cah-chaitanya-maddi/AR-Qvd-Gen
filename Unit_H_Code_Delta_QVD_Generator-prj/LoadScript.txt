///$tab Doc
/*
 ******************************************************************************
 * Overview:
 *   This is a QVD generator for UNIT_H_CODE Delta information.
 *   Put some usefull description here. 
 * 
 * Sources:
 *   Teradata
 *
 * Outputs:
 *   Unit_H_Code.qvd
 ******************************************************************************
 *
 * Revision History:
 * 
 * 07/17/2013 Joe Fusco
 * changed where not exists from qvd load from Package U/M Code-hist to UNIT_H_CDE
 *
 * 03/03/13 Dennis Duncan
 * Added registry lookup for root folder
 * Changed QVD paths to use vQVDs variable
 * Changed ODBC connection to use vSalesDataWarehouseConnection variable
 * Added meta-data for QlikView names
 * 
 * 03/02/13 Dennis Duncan
 * Added the Doc tab
 * Added the Setup tab
 * Added the include for Defaults.txt script
 * Added the include for File_Locations.txt script
 * Added the include for DB_Connections.txt script
 *
 * 04/17/13 Aaron Zechman
 * Initial Creation
 */
///$tab Setup
LET vRootFileLocation    = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'RootFolderLocation');
LET vExtractFileLocation = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView\Extract_Files', 'RootFolderLocation');
LET vEnvironment = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'Environment');

$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Defaults.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\File_Locations.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\DB_Connections.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Web.qvs);
$(Must_Include=$(vRootFileLocation)\$(vScriptDir)\Wait_Until_Writable.qvs);
$(Must_Include=$(vRootFileLocation)\$(vScriptDir)\Util.qvs);

ODBC CONNECT TO '$(vSalesDataWarehouseConnection)';
///$tab Unit_H_Code
/********************************************
 * Set Statistics Variables
 ********************************************/
LET vTimeStamp = Now();
LET vStart = Now();
SET vTableName = 'Unit_H_Code';
set vQvdFile = '$(vRootFileLocation)$(vQVDs)$(vTableName).qvd';

/********************************************
 * Get Last Time QVD was Generated and Current Time
 ********************************************/
LET vThisExecTime = date(Now(),'YYYY-MM-DD hh:mm:ss'); 

/********************************************
 * Get Latest Date from QVD
 ********************************************/
Max_Date:
LOAD
  max(ROW_UPDATE_STP) as max_date
FROM
  '$(vQvdFile)'
(qvd);

/********************************************
 * Set vLastExecTime
 ********************************************/
LET vLastExecTime = date(Peek('max_date',0,'Max_Date'),'YYYY-MM-DD hh:mm:ss');

/********************************************
 * Drop Table
 ********************************************/
DROP TABLE Max_Date;

/********************************************
 * Get Changed/Updated Records
 ********************************************/
Unit_H_Code:
LOAD
  UNIT_H_CDE
 ,Capitalize(UNIT_H_DESC) as [Package U/M Description]
 ,UNIT_H_ID               as [Package U/M]
 ,ROW_ADD_STP
 ,ROW_ADD_USER_ID
 ,ROW_UPDATE_STP
 ,ROW_UPDATE_USER_ID
;
SQL
SELECT
  UNIT_H_CDE
 ,UNIT_H_DESC
 ,UNIT_H_ID
 ,ROW_ADD_STP
 ,ROW_ADD_USER_ID
 ,ROW_UPDATE_STP
 ,ROW_UPDATE_USER_ID
FROM
  VIEWS.UNIT_H_CODE
WHERE  
  (ROW_UPDATE_STP > cast('$(vLastExecTime)' as timestamp))
//  OR (ROW_ADD_STP >= '$(vLastExecTime)'
//  AND ROW_ADD_STP < '$(vThisExecTime)')  
;

/********************************************
 * Check to see if any new records exists
 * otherwise do not concatenate
 ********************************************/
LET vDeltaRecords = NoOfRows('$(vTableName)');

IF $(vDeltaRecords) <> 0 THEN
  /********************************************
   * Concatenate Records where not exists in
   * the query above
   ********************************************/
  CONCATENATE
  LOAD
    ROW_UPDATE_USER_ID
   ,ROW_UPDATE_STP
   ,ROW_ADD_USER_ID
   ,ROW_ADD_STP
   ,[Package U/M]
   ,[Package U/M Description]
   ,[UNIT_H_CDE]
  FROM
	'$(vQvdFile)' (qvd)
  WHERE
    NOT EXISTS([UNIT_H_CDE], [UNIT_H_CDE])
  ;

  /********************************************
   * Store New QVD, if no errors
   ********************************************/ 
  call store_into_qvd(vTableName, vQvdFile, 0);
  let vLastExecTime = '$(vThisExecTime)';
  
  /********************************************
   * Store Dev QVD
   ********************************************/
	if(wildmatch('$(vEnvironment)','*DEV*')) > 0 then

		trace "************************** DEV LOAD: Dev copy for Unit_H_Code";

		store 
			Unit_H_Code
		into 
			'$(vRootFileLocation)$(vQVDs)\Dev\Unit_H_Code.qvd';
	
	endif;
 
END IF


/********************************************
 * Drop table out of memory
 ********************************************/ 
call drop_table(vTableName);

