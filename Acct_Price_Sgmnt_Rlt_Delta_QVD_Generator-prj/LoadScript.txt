///$tab Doc
/*
 ******************************************************************************
 * Overview:
 *   This is a QVD generator for ACCT_PRICE_SGMNT_RLT Delta information.
 *   Put some usefull description here. 
 * 
 * Sources:
 *   Teradata
 *
 * Outputs:
 *   Acct_Price_Sgmnt_Rlt.qvd
 ******************************************************************************
 *
 * Revision History:
 * 
 * 03/03/13 Dennis Duncan
 * Added registry lookup for root folder
 * Changed QVD paths to use vQVDs variable
 * Changed ODBC connection to use vSalesDataWarehouseConnection variable
 * Added meta-data for QlikView names
 * 
 * 03/02/13 Dennis Duncan
 * Added the Doc tab
 * Added the Setup tab
 * Added the include for Defaults.txt script
 * Added the include for File_Locations.txt script
 * Added the include for DB_Connections.txt script
 *
 * 04/15/13 Aaron Zechman
 * Initial Creation
 */
///$tab Setup
LET vRootFileLocation    = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'RootFolderLocation');
LET vExtractFileLocation = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView\Extract_Files', 'RootFolderLocation');
LET vEnvironment = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'Environment');

$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Defaults.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\File_Locations.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\DB_Connections.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Web.qvs);

ODBC CONNECT TO '$(vSalesDataWarehouseConnection)';
///$tab Acct_Price_Sgmnt_Rlt
/********************************************
 * Set Statistics Variables
 ********************************************/
LET vTimeStamp = Now();
LET vStart = Now();
SET vTableName = 'Acct_Price_Sgmnt_Rlt';

/********************************************
 * Get Last Time QVD was Generated and Current Time
 ********************************************/
LET vThisExecTime = date(Now(),'YYYY-MM-DD hh:mm:ss'); 

/********************************************
 * Get List of Dates
 ********************************************/
temp:
LOAD
  ROW_UPDATE_STP as Date
FROM
  '$(vRootFileLocation)$(vQVDs)$(vTableName).qvd'
(qvd);

/********************************************
 * Get Max Date
 ********************************************/
Max_Date:
Load
  min(Date) as min_date,
  max(Date) as max_date;
Load
  FieldValue('Date',IterNo()) as Date
autogenerate(1)
while not IsNull(FieldValue('Date',Iterno()));

/********************************************
 * Drop Table
 ********************************************/
Drop Table temp;

/********************************************
 * Set vLastExecTime
 ********************************************/
LET vLastExecTime = date(Peek('max_date',0,'Max_Date'),'YYYY-MM-DD hh:mm:ss');

/********************************************
 * Drop Table
 ********************************************/
DROP TABLE Max_Date; 

/********************************************
 * Get Changed/Updated Records
 ********************************************/
Acct_Price_Sgmnt_Rlt:
LOAD
  PRICE_SGMNT_CDE & '-' & START_DTE & '-' & SHIP_TO_CUSTOMER_NUM & '-' & SHIP_TO_LOCATION_NUM as [PRIMARY_KEY]
 ,PRICE_SGMNT_CDE                                                                             as [PRICE_SGMNT_CDE]
 ,SHIP_TO_LOCATION_NUM                                                                        as [SHIP_TO_LOCATION_NUM]
 ,SHIP_TO_CUSTOMER_NUM                                                                        as [SHIP_TO_CUSTOMER_NUM]
 ,CCDB_STAT_CDE                                                                               as [CCDB_STAT_CDE]
 ,START_DTE                                                                                   as [START_DTE]
 ,SEQ_KEY_NUM                                                                                 as [SEQ_KEY_NUM]
 ,END_DTE                                                                                     as [END_DTE]
 ,PRIORITY_NUM                                                                                as [PRIORITY_NUM]
 ,CNTRCT_MULT_NUM                                                                             as [CNTRCT_MULT_NUM]
 ,NON_CNTRCT_MULT_NUM                                                                         as [Discount %]
 ,ROW_ADD_STP                                                                                 as [ROW_ADD_STP]
 ,ROW_ADD_USER_ID                                                                             as [ROW_ADD_USER_ID]
 ,ROW_UPDATE_STP                                                                              as [ROW_UPDATE_STP]
 ,ROW_UPDATE_USER_ID                                                                          as [ROW_UPDATE_USER_ID]
;
SQL
SELECT
  PRICE_SGMNT_CDE
 ,SHIP_TO_LOCATION_NUM
 ,SHIP_TO_CUSTOMER_NUM
 ,CCDB_STAT_CDE
 ,START_DTE
 ,SEQ_KEY_NUM
 ,END_DTE
 ,PRIORITY_NUM
 ,CNTRCT_MULT_NUM
 ,NON_CNTRCT_MULT_NUM
 ,ROW_ADD_STP
 ,ROW_ADD_USER_ID
 ,ROW_UPDATE_STP
 ,ROW_UPDATE_USER_ID
FROM
  VIEWS.ACCT_PRICE_SGMNT_RLT
WHERE  
  (ROW_UPDATE_STP > cast('$(vLastExecTime)' as timestamp));
//  OR (ROW_ADD_STP >= '$(vLastExecTime)'
//  AND ROW_ADD_STP < '$(vThisExecTime)') 

/********************************************
 * Check to see if any new records exists
 * otherwise do not concatenate
 ********************************************/
LET vDeltaRecords = NoOfRows('$(vTableName)');

IF $(vDeltaRecords) <> 0 THEN
  /********************************************
   * Concatenate Records where not exists in
   * the query above
   ********************************************/
  CONCATENATE
  LOAD
    ROW_UPDATE_USER_ID
   ,ROW_UPDATE_STP
   ,ROW_ADD_USER_ID
   ,ROW_ADD_STP
   ,[Discount %]
   ,CNTRCT_MULT_NUM
   ,PRIORITY_NUM
   ,END_DTE
   ,SEQ_KEY_NUM
   ,START_DTE
   ,CCDB_STAT_CDE
   ,SHIP_TO_CUSTOMER_NUM
   ,SHIP_TO_LOCATION_NUM
   ,PRICE_SGMNT_CDE
  FROM
   '$(vRootFileLocation)$(vQVDs)Acct_Price_Sgmnt_Rlt.qvd' (qvd)
  WHERE
    NOT EXISTS(PRIMARY_KEY, PRICE_SGMNT_CDE & '-' & START_DTE & '-' & SHIP_TO_CUSTOMER_NUM & '-' & SHIP_TO_LOCATION_NUM)
  ;

  /********************************************
   * Since Primary Key was composite key need
   * to drop the field
   ********************************************/
  DROP FIELD PRIMARY_KEY;

  /********************************************
   * Store New QVD, if no errors
   ********************************************/ 
  IF ScriptErrorCount = 0 THEN
    STORE
      Acct_Price_Sgmnt_Rlt
    INTO
      '$(vRootFileLocation)$(vQVDs)Acct_Price_Sgmnt_Rlt.qvd';
    LET vLastExecTime = '$(vThisExecTime)'; 
  END IF
END IF

/********************************************
 * Set Statistics Variables
 ********************************************/
LET vEnd = Now();
LET vNoOfRows = NoOfRows('$(vTableName)');
LET vNoOfFields = NoOfFields('$(vTableName)');
LET vFileSize = FileSize('$(vRootFileLocation)$(vQVDs)$(vTableName).qvd');
LET vDuration = interval(num(vEnd)-num(vStart),'hh:mm:ss');

/********************************************
 * Drop table out of memory
 ********************************************/ 
DROP TABLE Acct_Price_Sgmnt_Rlt;

/********************************************
 * Load Statistics in Memory
 ********************************************/
LoadStats:
load
	year('$(vTimeStamp)')  as LoadYear,
	month('$(vTimeStamp)') as LoadMonth,
	day('$(vTimeStamp)')   as LoadDay,	
	'$(vTableName)'		   as TableName,
	'$(vStart)'			   as DateStart,
	'$(vEnd)'			   as DateEnd,
	'$(vNoOfFields)'       as FieldCount, 	
	'$(vNoOfRows)'		   as RecordCount,
	'$(vDuration)'         as LoadDuration,
	'$(vQVDs)'             as QVDPath
autogenerate 1;

/********************************************
 * Check to see if QVD exists 
 ********************************************/
IF (NOT isNull(qvdCreateTime('$(vRootFileLocation)$(vQVDs)LoadStats.qvd'))) THEN
  CONCATENATE(LoadStats)
  LOAD
    *
  FROM
    '$(vRootFileLocation)$(vQVDs)LoadStats.qvd'(qvd);
END IF

/********************************************
 * Store Statistics
 ********************************************/
STORE
  LoadStats
INTO
  '$(vRootFileLocation)$(vQVDs)LoadStats.qvd'(qvd);