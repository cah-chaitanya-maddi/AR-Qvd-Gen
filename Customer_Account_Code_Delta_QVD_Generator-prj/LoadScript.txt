///$tab Doc
/*
 ******************************************************************************
 * Overview:
 *   This is a QVD generator for CUSTOMER_ACCOUNT_CODE Delta information.
 *   Put some usefull description here. 
 * 
 * Sources:
 *   Teradata
 *
 * Outputs:
 *   Customer_Account_Code.qvd
 ******************************************************************************
 *
 * Revision History:
 * 
 * 03/03/13 Dennis Duncan
 * Added registry lookup for root folder
 * Changed QVD paths to use vQVDs variable
 * Changed ODBC connection to use vSalesDataWarehouseConnection variable
 * Added meta-data for QlikView names
 * 
 * 03/02/13 Dennis Duncan
 * Added the Doc tab
 * Added the Setup tab
 * Added the include for Defaults.txt script
 * Added the include for File_Locations.txt script
 * Added the include for DB_Connections.txt script
 *
 * 04/16/13 Aaron Zechman
 * Initial Creation
 */
///$tab Setup
LET vRootFileLocation    = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'RootFolderLocation');
LET vExtractFileLocation = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView\Extract_Files', 'RootFolderLocation');
LET vEnvironment = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'Environment');

$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Defaults.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\File_Locations.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\DB_Connections.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Web.qvs);

ODBC CONNECT TO '$(vSalesDataWarehouseConnection)';
//ODBC CONNECT TO PD_SDW_Stg;
//VIEWST Changes by Sumit 11/06/2020 ARR 262-264
///$tab Customer_Account_Code
/********************************************
 * Set Statistics Variables
 ********************************************/
LET vTimeStamp = Now();
LET vStart = Now();
SET vTableName = 'Customer_Account_Code';

/********************************************
 * Get Last Time QVD was Generated and Current Time
 ********************************************/
LET vThisExecTime = date(Now(),'YYYY-MM-DD hh:mm:ss'); 

/********************************************
 * Get List of Dates
 ********************************************/
temp:
LOAD
  ROW_UPDATE_STP as Date
FROM
  '$(vRootFileLocation)$(vQVDs)$(vTableName).qvd'
(qvd);

/********************************************
 * Get Max Date
 ********************************************/
Max_Date:
Load
  min(Date) as min_date,
  max(Date) as max_date;
Load
  FieldValue('Date',IterNo()) as Date
autogenerate(1)
while not IsNull(FieldValue('Date',Iterno()));

/********************************************
 * Drop Table
 ********************************************/
Drop Table temp;

/********************************************
 * Set vLastExecTime
 ********************************************/
LET vLastExecTime = date(Peek('max_date',0,'Max_Date'),'YYYY-MM-DD hh:mm:ss');

/********************************************
 * Drop Table
 ********************************************/
DROP TABLE Max_Date; 

/********************************************
 * Get Changed/Updated Records
 ********************************************/
Customer_Account_Code:
LOAD
  CAC_ID             as [Customer Item Code]
 ,CAC_KEY_NUM        as [CAC_KEY_NUM]
 ,ROW_ADD_STP        as [ROW_ADD_STP]
 ,ROW_ADD_USER_ID    as [ROW_ADD_USER_ID]
 ,ROW_UPDATE_STP     as [ROW_UPDATE_STP]
 ,ROW_UPDATE_USER_ID as [ROW_UPDATE_USER_ID]
;
SQL
SELECT
  CAC_ID
 ,CAC_KEY_NUM
 ,ROW_ADD_STP
 ,ROW_ADD_USER_ID
 ,ROW_UPDATE_STP
 ,ROW_UPDATE_USER_ID
FROM
  VIEWST.CUSTOMER_ACCOUNT_CODE
WHERE  
  (ROW_UPDATE_STP > cast('$(vLastExecTime)' as timestamp))
//  OR (ROW_ADD_STP >= '$(vLastExecTime)'
//  AND ROW_ADD_STP < '$(vThisExecTime)')  
;

/********************************************
 * Check to see if any new records exists
 * otherwise do not concatenate
 ********************************************/
LET vDeltaRecords = NoOfRows('$(vTableName)');

IF $(vDeltaRecords) <> 0 THEN
  /********************************************
   * Concatenate Records where not exists in
   * the query above
   ********************************************/
  CONCATENATE
  LOAD
    ROW_UPDATE_USER_ID
   ,ROW_UPDATE_STP
   ,ROW_ADD_USER_ID
   ,ROW_ADD_STP
   ,CAC_KEY_NUM
   ,[Customer Item Code]
  FROM
    '$(vRootFileLocation)$(vQVDs)Customer_Account_Code.qvd' (qvd)
  WHERE
    NOT EXISTS(CAC_KEY_NUM, CAC_KEY_NUM)
  ;

  /********************************************
   * Store New QVD, if no errors
   ********************************************/ 
  IF ScriptErrorCount = 0 THEN
    STORE
      Customer_Account_Code
    INTO
      '$(vRootFileLocation)$(vQVDs)Customer_Account_Code.qvd';
    LET vLastExecTime = '$(vThisExecTime)'; 
  END IF
  
	/********************************************
	 * Store Dev QVD
	 ********************************************/
	if(wildmatch('$(vEnvironment)','*DEV*')) > 0 then

	trace "************************** DEV LOAD: Dev copy for Customer_Account_Code";

		store 
			Customer_Account_Code
		into 
			'$(vRootFileLocation)$(vQVDs)\Dev\Customer_Account_Code.qvd';
	
	endif;
  
END IF

/********************************************
 * Set Statistics Variables
 ********************************************/
LET vEnd = Now();
LET vNoOfRows = NoOfRows('$(vTableName)');
LET vNoOfFields = NoOfFields('$(vTableName)');
LET vFileSize = FileSize('$(vRootFileLocation)$(vQVDs)$(vTableName).qvd');
LET vDuration = interval(num(vEnd)-num(vStart),'hh:mm:ss');

/********************************************
 * Drop table out of memory
 ********************************************/ 
DROP TABLE Customer_Account_Code;

/********************************************
 * Load Statistics in Memory
 ********************************************/
LoadStats:
load
	year('$(vTimeStamp)')  as LoadYear,
	month('$(vTimeStamp)') as LoadMonth,
	day('$(vTimeStamp)')   as LoadDay,	
	'$(vTableName)'		   as TableName,
	'$(vStart)'			   as DateStart,
	'$(vEnd)'			   as DateEnd,
	'$(vNoOfFields)'       as FieldCount, 	
	'$(vNoOfRows)'		   as RecordCount,
	'$(vDuration)'         as LoadDuration,
	'$(vQVDs)'             as QVDPath
autogenerate 1;

/********************************************
 * Check to see if QVD exists 
 ********************************************/
IF (NOT isNull(qvdCreateTime('$(vRootFileLocation)$(vQVDs)LoadStats.qvd'))) THEN
  CONCATENATE(LoadStats)
  LOAD
    *
  FROM
    '$(vRootFileLocation)$(vQVDs)LoadStats.qvd'(qvd);
END IF

/********************************************
 * Store Statistics
 ********************************************/
STORE
  LoadStats
INTO
  '$(vRootFileLocation)$(vQVDs)LoadStats.qvd'(qvd);
  
/********************************************
 * Drop Table
 ********************************************/
DROP TABLE LoadStats;  