///$tab Doc
/*
 ******************************************************************************
 * Overview:
 *   This is a QVD generator for CONTRACT_DETAIL_D1 information.
 *   Put some usefull description here. 
 * 
 * Sources:
 *   Teradata
 *
 * Outputs:
 *   Contract_Detail_D1.qvd
 ******************************************************************************
 *
 * Revision History:
 * 
 * 03/03/13 Dennis Duncan
 * Added registry lookup for root folder
 * Changed QVD paths to use vQVDs variable
 * Changed ODBC connection to use vSalesDataWarehouseConnection variable
 * Added meta-data for QlikView names
 * 
 * 03/02/13 Dennis Duncan
 * Added the Doc tab
 * Added the Setup tab
 * Added the include for Defaults.txt script
 * Added the include for File_Locations.txt script
 * Added the include for DB_Connections.txt script
 *
 * 05/13/13 Aaron Zechman
 * Initial Creation
 */
///$tab Setup
LET vRootFileLocation    = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'RootFolderLocation');
LET vExtractFileLocation = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView\Extract_Files', 'RootFolderLocation');
LET vEnvironment = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'Environment');

$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Defaults.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\File_Locations.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\DB_Connections.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Web.qvs);
$(Must_Include=$(vRootFileLocation)\$(vScriptDir)\Wait_Until_Writable.qvs);
$(Must_Include=$(vRootFileLocation)\$(vScriptDir)\Util.qvs);

//ODBC CONNECT TO '$(vSalesDataWarehouseConnection)';
ODBC CONNECT TO PD_SDW_Stg;
//VIEWS Changes by Sumit 11/06/2020 ARR 262-264
///$tab Contract_Detail_D1
/********************************************
 * Set Statistics Variables
 ********************************************/
LET vTimeStamp = Now();
LET vStart = Now();
SET vTableName = 'Contract_Detail_D1';
set vQvdFile = '$(vRootFileLocation)$(vQVDs)$(vTableName).qvd';
LET vThisExecTime = date(Now(),'YYYY-MM-DD hh:mm:ss'); 
/********************************************
 * Get List of Dates
 ********************************************/
temp:
LOAD
  ROW_UPDATE_STP as Date
FROM
  '$(vQvdFile)'
(qvd);

/********************************************
 * Get Max Date
 ********************************************/
Max_Date:
Load
  min(Date) as min_date,
  max(Date) as max_date;
Load
  FieldValue('Date',IterNo()) as Date
autogenerate(1)
while not IsNull(FieldValue('Date',Iterno()));

/********************************************
 * Drop Table
 ********************************************/
Drop Table temp;

/********************************************
 * Set vLastExecTime
 ********************************************/
LET vLastExecTime = date(Peek('max_date',0,'Max_Date'),'YYYY-MM-DD hh:mm:ss');

/********************************************
 * Drop Table
 ********************************************/
DROP TABLE Max_Date;

/********************************************
 * Load Table into Memory
 ********************************************/
Contract_Detail_D1:
LOAD
  CORP_ITEM_NUM & '-' & CNTRCT_NUM as [PRIMARY_KEY]
 ,CORP_ITEM_NUM                    as [CORP_ITEM_NUM]
 ,CNTRCT_NUM                       as [CNTRCT_NUM]
 ,START_DTE                        as [START_DTE]
 ,END_DTE                          as [END_DTE]
 ,CNTRCT_SELL_DLR                  as [CNTRCT_SELL_DLR]
 ,CNTRCT_COST_DLR                  as [CNTRCT_COST_DLR]
 ,ROW_ADD_STP                      as [ROW_ADD_STP]
 ,ROW_ADD_USER_ID                  as [ROW_ADD_USER_ID]
 ,ROW_UPDATE_STP                   as [ROW_UPDATE_STP]
 ,ROW_UPDATE_USER_ID               as [ROW_UPDATE_USER_ID]
;
SQL
SELECT
  CORP_ITEM_NUM
 ,CNTRCT_NUM
 ,START_DTE
 ,END_DTE
 ,CNTRCT_SELL_DLR
 ,CNTRCT_COST_DLR
 ,ROW_ADD_STP
 ,ROW_ADD_USER_ID
 ,ROW_UPDATE_STP
 ,ROW_UPDATE_USER_ID
FROM
  VIEWS.CONTRACT_DETAIL_D1
WHERE
  (ROW_UPDATE_STP > cast('$(vLastExecTime)' as timestamp))  
;

LET vDeltaRecords = NoOfRows('$(vTableName)');

IF $(vDeltaRecords) <> 0 THEN
  /********************************************
   * Concatenate Records where not exists in
   * the query above
   ********************************************/
  CONCATENATE
  LOAD
    CORP_ITEM_NUM
   ,CNTRCT_NUM
   ,START_DTE
   ,END_DTE
   ,CNTRCT_SELL_DLR
   ,CNTRCT_COST_DLR
   ,ROW_ADD_STP
   ,ROW_ADD_USER_ID
   ,ROW_UPDATE_STP
   ,ROW_UPDATE_USER_ID
  FROM
   '$(vQvdFile)' (qvd)
  WHERE
    NOT EXISTS(PRIMARY_KEY,CORP_ITEM_NUM & '-' & CNTRCT_NUM)
  ;

  /********************************************
   * Since Primary Key was composite key need
   * to drop the field
   ********************************************/
  DROP FIELD PRIMARY_KEY;

  /********************************************
   * Store In-Memory Table into QVD
   ********************************************/
	STORE Contract_Detail_D1 into '$(vRootFileLocation)$(vQVDs)Contract_Detail_D1.qvd';
	//call store_into_qvd(vTableName, vQvdFile, 0);
    let vLastExecTime = '$(vThisExecTime)'; 
END IF


 /********************************************
  * Free up memory
  ********************************************/
DROP Table Contract_Detail_D1;
//call drop_table(vTableName);
