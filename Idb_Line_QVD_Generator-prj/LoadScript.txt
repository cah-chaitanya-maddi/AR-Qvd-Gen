///$tab Doc
/*
 ******************************************************************************
 * Overview:
 *   This is a QVD generator for IDB_LINE information.
 *   Put some usefull description here. 
 * 
 * Sources:
 *   Teradata
 *
 * Outputs:
 *   Idb_Line.qvd
 ******************************************************************************
 *
 * Revision History:
 * 
 * 03/03/13 Dennis Duncan
 * Added registry lookup for root folder
 * Changed QVD paths to use vQVDs variable
 * Changed ODBC connection to use vSalesDataWarehouseConnection variable
 * Added meta-data for QlikView names
 * 
 * 03/02/13 Dennis Duncan
 * Added the Doc tab
 * Added the Setup tab
 * Added the include for Defaults.txt script
 * Added the include for File_Locations.txt script
 * Added the include for DB_Connections.txt script
 *
 * 02/27/13 Aaron Zechman
 * Initial Creation
 */
///$tab Setup
LET vRootFileLocation    = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'RootFolderLocation');
LET vExtractFileLocation = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView\Extract_Files', 'RootFolderLocation');
LET vEnvironment = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'Environment');

$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Defaults.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\File_Locations.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\DB_Connections.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Web.qvs);
///$tab Idb_Entry_Limiter
//*****************************************************************
trace "************************** Checking Environment setting";
//*****************************************************************
if(wildmatch('$(vEnvironment)','*DEV*')) > 0 then

       //*****************************************************************
       trace "************************** DEV LOAD: Loading Ship_To_Limiter table";
       //*****************************************************************
       Idb_Entry_Limiter:
       load
              BILL_ENTRY_KEY_NUM as BILL_ENTRY_KEY_NUM_FILTER
       from
              '$(vRootFileLocation)$(vQVDs)\Dev\Idb_Entry.qvd'
              (qvd)
       ;

endif;
///$tab Idb_Line
/********************************************
 * Set Statistics Variables
 ********************************************/
LET vTimeStamp = Now();
LET vStart = Now();
SET vTableName = 'Idb_Line';

/********************************************
 * Load Table into Memory
 ********************************************/
Idb_Line_Temp:
LOAD
  BILL_ENTRY_KEY_NUM                                                                     as [BILL_ENTRY_KEY_NUM]
 ,BILL_ENTRY_LINE_NUM                                                                    as [BILL_ENTRY_LINE_NUM]
 ,IDB_PROD_KEY_NUM                                                                       as [IDB_PROD_KEY_NUM]
 ,SRC_DOC_KEY_NUM                                                                        as [SRC_DOC_KEY_NUM]
 ,FLG_KEY_NUM                                                                            as [FLG_KEY_NUM]
 ,EXP_DTE_KEY_NUM                                                                        as [EXP_DTE_KEY_NUM]
 ,SEQ_NUM                                                                                as [SEQ_NUM]
 ,LOT_ID                                                                                 as [LOT_ID]
 ,UNIT_COST_DLR                                                                          as [UNIT_COST_DLR]
 ,UOI_COST_DLR                                                                           as [UOI_COST_DLR]
 ,PURCH_QTY                                                                              as [PURCH_QTY]
 ,EXT_COST_DLR                                                                           as [EXT_COST_DLR]
 ,MARKUP_DLR                                                                             as [MARKUP_DLR]
 ,DSPNS_FEE_DLR                                                                          as [DSPNS_FEE_DLR]
 ,EXT_FEE_DLR                                                                            as [EXT_FEE_DLR]
 ,PACK_QTY                                                                               as [PACK_QTY]
 ,PACK_SIZE_QTY                                                                          as [PACK_SIZE_QTY]
 ,UNIT_H_ID                                                                              as [UNIT_H_ID]
 ,UOIF_NUM                                                                               as [UOIF_NUM]
 ,Capitalize(DSPNS_SIZE_TXT)                                                             as [DSPNS_SIZE_TXT]
 ,upper(date(date#(ROW_ADD_STP,'YYYY-MM-DD hh:mm:ss.ffffff'),'M/D/YYYY hh:mm:ss tt'))    as [ROW_ADD_STP] 
 ,ROW_ADD_USER_ID                                                                        as [ROW_ADD_USER_ID]
 ,upper(date(date#(ROW_UPDATE_STP,'YYYY-MM-DD hh:mm:ss.ffffff'),'M/D/YYYY hh:mm:ss tt')) as [ROW_UPDATE_STP]
 ,ROW_UPDATE_USER_ID                                                                     as [ROW_UPDATE_USER_ID]
 ,DEL_FLG																				 as [DEL_FLG]
FROM
  '$(vExtractFileLocation)IDB_LINE-Caret.txt'
  (txt, codepage is 1252, embedded labels, delimiter is '^', no quotes, no eof)
;


NoConcatenate
Idb_Line:
Load *
Resident Idb_Line_Temp
Where 
DEL_FLG='N';

DROP Table Idb_Line_Temp;

/********************************************
 * Store In-Memory Table into QVD
 ********************************************/
STORE
  Idb_Line
INTO
  '$(vRootFileLocation)$(vQVDs)Idb_Line.qvd' (qvd)
;

/********************************************
 * Store Dev QVD
 ********************************************/
if(wildmatch('$(vEnvironment)','*DEV*')) > 0 then

	trace "************************** DEV LOAD: Creating Dev version of <<Table_Name>>";

	NoConcatenate
	Idb_Line_dev:
	load 
		* 
	resident
		Idb_Line
	where exists(BILL_ENTRY_KEY_NUM_FILTER, BILL_ENTRY_KEY_NUM)
	;

	store 
		Idb_Line_dev
	into
		'$(vRootFileLocation)$(vQVDs)\Dev\Idb_Line.qvd';
	
	drop table Idb_Line_dev;
	
endif;


/********************************************
 * Free up memory
 ********************************************/
DROP TABLE
  Idb_Line
;
