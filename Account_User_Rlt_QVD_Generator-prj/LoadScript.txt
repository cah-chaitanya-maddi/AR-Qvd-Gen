///$tab Doc
/*
 ******************************************************************************
 * Overview:
 *   This is a QVD generator for ACCOUNTS_USER_RLT information.
 *   Put some usefull description here. 
 * 
 * Sources:
 *   Teradata
 *
 * Outputs:
 *   Account_User_Rlt.qvd
 ******************************************************************************
 * Overview:
 *   This is a QVD generator for ACCOUNT_USER_RLT information.
 *   Loads User Accounts information from the ACCOUNT_USER_RLT-Caret.txt file
 *   based on the ACCOUNT_KEY_NUM existing in the Ship_To_Account.qvd which
 *   has been filtered to remove Walgreens and CVS.
 * 
 * Sources:
 *   ACCOUNT_USER_RLT-Caret.txt
 *   Ship_To_Account.qvd
 *
 * Outputs:
 *   Account_User_Rlt.qvd
 ******************************************************************************
 * Revision History:
 * 
 * 06/24/2013 Joe Roberts
 * Added logic to filter by ACCOUNT_KEY_NUM existing in Ship_To_Account.qvd
 *
 * 03/03/13 Dennis Duncan
 * Added registry lookup for root folder
 * Changed QVD paths to use vQVDs variable
 * Changed ODBC connection to use vSalesDataWarehouseConnection variable
 * Added meta-data for QlikView names
 * 
 * 03/02/13 Dennis Duncan
 * Added the Doc tab
 * Added the Setup tab
 * Added the include for Defaults.txt script
 * Added the include for File_Locations.txt script
 * Added the include for DB_Connections.txt script
 *
 * 03/27/13 Aaron Zechman
 * Initial Creation
 */
///$tab Setup
LET vRootFileLocation    = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'RootFolderLocation');
LET vExtractFileLocation = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView\Extract_Files', 'RootFolderLocation');LET vEnvironment = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'Environment');
LET vEnvironment = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'Environment');

$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Defaults.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\File_Locations.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\DB_Connections.qvs);
///$tab SHip To Limiter
//*****************************************************************
trace "************************** Checking Environment setting";
//*****************************************************************
if(wildmatch('$(vEnvironment)','*DEV*')) > 0 then

       //*****************************************************************
       trace "************************** DEV LOAD: Loading Ship_To_Limiter table";
       //*****************************************************************
       Ship_To_Limiter:
       load
              ACCT_KEY_NUM as ACCT_KEY_NUM_FILTER
       from
              '$(vRootFileLocation)$(vQVDs)\Dev\Ship_To_Account.qvd'
              (qvd)
       ;

endif;
///$tab Account_User_Rlt
/*****************************************************************
Create a QVD containing Account Key Num to Mercury User ID relationship.
Loaded from an initial TPT extract file.
*****************************************************************/


/*********************************************
 * Load Ship_To_Account.qvd to filter out
 * User accounts asigned to Walgreens or CVS
*********************************************/

Ship_To_Account:
load 
  distinct ACCT_KEY_NUM
from
  '$(vRootFileLocation)$(vQVDs)Ship_To_Account.qvd'
(qvd);

/********************************************
 * Load Table into Memory
 ********************************************/
Account_User_Rlt:
load distinct
  ACCT_KEY_NUM
 ,Upper(MRCRY_USER_ID) as MRCRY_USER_ID     //changed on 5/12 will be deployed on 5/15/2020
 ,ROW_ADD_STP 
 ,ROW_ADD_USER_ID
 ,ROW_UPDATE_STP 
 ,ROW_UPDATE_USER_ID
from
  '$(vExtractFileLocation)ACCOUNT_USER_RLT-Caret.txt'
  (txt, codepage is 1252, embedded labels, delimiter is '^', no quotes, no eof)
  // Added to remove CVS and Walgreens
where
  exists(ACCT_KEY_NUM)
;

/********************************************
 * Store In-Memory Table into QVD
 ********************************************/
 store Account_User_Rlt into '$(vRootFileLocation)$(vQVDs)Account_User_Rlt.qvd' (qvd);

if(wildmatch('$(vEnvironment)','*DEV*')) > 0 then

	//*****************************************************************
	trace "************************** DEV LOAD: Creating Dev version of Account_User_Rlt";
	//*****************************************************************
	NoConcatenate
	Account_User_Rlt_dev:
	load 
		* 
	resident
		Account_User_Rlt
	where exists(ACCT_KEY_NUM_FILTER, ACCT_KEY_NUM)
	;

	store Account_User_Rlt_dev into '$(vRootFileLocation)$(vQVDs)\Dev\Account_User_Rlt.qvd';
	drop table Account_User_Rlt_dev;
	
endif;

drop table Account_User_Rlt;
drop table Ship_To_Account;

