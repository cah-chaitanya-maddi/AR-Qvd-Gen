///$tab Doc
/*
 ******************************************************************************
 * Overview:
 *   This is a QVD generator for Tier 2 Invoice_Line information.
 *   This table is a combination of Invoice_Line_YYYYMM, Flag, 
 *   tables
 * 
 * Sources:
 *	 Invoice_Line_*.qvd 	
 * Outputs:
 *   Invoice_Line.qvd
 ******************************************************************************
 *
 * Revision History:
 *
 * 7/25/2013 Joe Roberts
 * Removed all logic except the concatenation of all historic files into a 
 * single Invoice Line file. All other logic moved to Invoice Line QVD
 * generator and Invoice Line delta QVD generator.
 *
 * 05/01/13 Aaron Zechman
 * Initial Creation
 */
///$tab Setup
/*********************************************************************************
Setup environment specific variables.
Load in defaults for formatting of currency,dates and numbers.

Changelog:
- 10.2.2013 jeremy.fourman
Removed inclusion of Web settings, those are for UI components only.
*********************************************************************************/

LET vRootFileLocation    = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'RootFolderLocation');
LET vExtractFileLocation = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView\Extract_Files', 'RootFolderLocation');
LET vEnvironment = GetRegistryString('HKEY_LOCAL_MACHINE\SOFTWARE\Cardinal Health\QlikView', 'Environment');

$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\Defaults.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\File_Locations.qvs);
$(Must_Include=$(vRootFileLocation)Config\$(vEnvironment)\DB_Connections.qvs);
$(Must_Include=$(vRootFileLocation)\$(vScriptDir)\Wait_Until_Writable.qvs);
$(Must_Include=$(vRootFileLocation)\$(vScriptDir)\Util.qvs);

//--


///$tab mapping
/***************************
Load Short Reason IDs for
Mapping to the Invoice Line
file for evaluation
***************************/
Short_Reason_Map:
Mapping Load 
 SHRT_REASON_KEY_NUM,
SHRT_ID
FROM
'$(vRootFileLocation)QVDs\Short_Reason.qvd'
(qvd);



//Order Date from Time Detail
OrderDate:
Mapping Load
   DTE_KEY_NUM as ORDER_DTE_KEY_NUM
  ,RFRNC_DTE as [Order Date]
from
	'$(vRootFileLocation)QVDs\TIME_DETAIL.qvd'
(qvd)
;
///$tab Ship_To_Filter
//*****************************************************************
trace "************************** Checking Environment setting";
//*****************************************************************
if(wildmatch('$(vEnvironment)','*DEV*')) > 0 then

       //*****************************************************************
       trace "************************** DEV LOAD: Loading Ship_To_Limiter table";
       //*****************************************************************
       Ship_To_Limiter:
       load
              ACCT_KEY_NUM as ACCT_KEY_NUM_FILTER
       from
              '$(vRootFileLocation)$(vQVDs)\Dev\Ship_To_Account.qvd'
              (qvd)
       ;

endif;
///$tab Invoice Line
/*********************************************************************************
Do any transformations on the fields within the Invoice_Line table and then write it 
back out.

Changelog:
- 8.26.2013 jeremy.fourman
Removed fields that do not need to be loaded at this time.
- 9.20.2013 jeremy.fourman
Removed the field [Short Invoice Code] CORP_ACCT_KEY_NUM was aliased to this
*********************************************************************************/

set vTableName = 'Invoice_Line_SLR';
set vQvdFile = '$(vRootFileLocation)$(vQVDs)$(vTableName).qvd';

// List the QVD files to be catenated
FOR EACH _file in filelist('$(vRootFileLocation)$(vQVDs)Invoice_Line_SLR_2*.qvd');
  ListFileTable:
  LOAD 
    subfield('$(_file)','\',substringcount('$(_file)','\')+1) as [File]
  AutoGenerate
      1
  ;
NEXT _file;

Trace Display qvd file names;
let v_files = NoOfRows('ListFileTable');
For vFileNo = 1 to v_files
      Let vFileName = Peek('File',vFileNo-1,'FileListTable');
      Trace $(vFileName);
Next vFileNo

Drop Table ListFileTable;

/******************************************************
*** Load all Invoice Line Monthly QVD files
******************************************************/

Invoice_Line_SLR:  
LOAD
	*
    
FROM
  '$(vRootFileLocation)$(vQVDs)Invoice_Line_SLR_2*.qvd'
(qvd);




Drop Fields CIN, [DC #], [Account #];



/***************************************************************
*** STORE THE COMBINED FILES TO A SINGLE FILE
***************************************************************/

//call store_into_qvd(vTableName, vQvdFile, 0);

STORE
   Invoice_Line_SLR
 INTO
   '$(vRootFileLocation)$(vQVDs)Invoice_Line_SLR.qvd' (qvd)
;

/********************************************
 * Store Dev QVD
 ********************************************/
if(wildmatch('$(vEnvironment)','*DEV*')) > 0 then

	trace "************************** DEV LOAD: Creating Dev version of Invoice_Line_SLR";

	NoConcatenate
	Invoice_Line_SLR_dev:
	load 
		* 
	resident
		Invoice_Line_SLR
	where exists(ACCT_KEY_NUM_FILTER, ACCT_KEY_NUM)
	;

	store 
		Invoice_Line_SLR_dev
	into
		'$(vRootFileLocation)$(vQVDs)\Dev\Invoice_Line_SLR.qvd';
	
	drop table Invoice_Line_SLR_dev;
	
endif;


drop table Invoice_Line_SLR;